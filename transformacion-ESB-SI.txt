<!--
Transformacion de ESB a SI
Linneker Carvajal 
Proyecto Modernizacion Banca en Linea
Octubre 2017
 -->
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" 
xmlns:dp="http://www.datapower.com/extensions"
xmlns:ns0="http://schemas.xmlsoap.org/soap/envelope/"
xmlns:xs="http://www.w3.org/2001/XMLSchema"
xmlns:ns1="urn:esbbancogeneral.com/gbo/xml/schemas/v1_0/"
xmlns:dcomcom="http://xmlns.bgeneral.com/ObjetoEmpresarial/DominioComun/Comun/V1"


extension-element-prefixes="dp"
exclude-result-prefixes="ns0 xs ns1 dp">
    <xsl:output method="xml" encoding="UTF-8" indent="yes"/>
    <xsl:template match="/">
        <!--Guardar header -->
        <dp:set-variable name="'var://context/saved/header'"
                         value="ns0:Envelope/ns0:Body/*/ns1:HEADER"/> 

        <!--Guardar perfil -->
        <dp:set-variable name="'var://context/saved/perfil'"
                         value="ns0:Envelope/ns0:Body/*/ns1:BODY/ns1:Perfil"/>

        <!--Buscar namespace destino -->
        <xsl:variable name="processorName" select="dp:variable('var://service/processor-name')" />
        <xsl:variable name="archivoMensajeria" select="document('local:///config/Mensajeria.xml')"/>
        <xsl:variable name="namespaceDestino" select="$archivoMensajeria/MensajesBase/uriMensajeria[@nombre-ws-proxy=$processorName]/@uri-mensaje"/>

        <!--Buscar operacion destino -->
        <xsl:variable name="operacionOrigen" select="local-name(ns0:Envelope/ns0:Body/*[1])"/>  
        <xsl:variable name="archivoOperaciones" select="document('local:///config/NombreOperacion.xml')"/>
        <xsl:variable name="operacionDestino" select="$archivoOperaciones/operaciones/operacion[@nombre-esb=substring-before($operacionOrigen,'MsjSol')]/@nombre-interno"/>

        <xsl:variable name="operacion">
            <xsl:choose>
                <xsl:when test="$operacionDestino">
                    <xsl:value-of select="concat($operacionDestino,'MsjSol')"/> 
                </xsl:when>
                <xsl:otherwise>
                    <xsl:value-of select="$operacionOrigen"/> 
                </xsl:otherwise>
            </xsl:choose>                
        </xsl:variable>

        <!--Transformar mensaje de ESB a SI/SEN -->
        <xsl:element name="out:{$operacion}" namespace="{$namespaceDestino}">
            <!--  <xsl:copy-of select="namespace::*"/> -->
            <xsl:for-each select="ns0:Envelope/ns0:Body/*/namespace::*">
                <xsl:copy-of select="."/>  
            </xsl:for-each> 

            <!--Transformar header-->
            <xsl:element name="CabeceraSol" namespace="{$namespaceDestino}" >
                <dcomcom:NumeroUnico>
                    <xsl:value-of select="ns0:Envelope/ns0:Body/*/ns1:HEADER/ns1:numeroUnico"/>
                </dcomcom:NumeroUnico>
                <dcomcom:Oficina>
                    <xsl:value-of select="ns0:Envelope/ns0:Body/*/ns1:HEADER/ns1:oficina"/>
                </dcomcom:Oficina>
                <dcomcom:RolUsuario>
                    <xsl:value-of select="ns0:Envelope/ns0:Body/*/ns1:HEADER/ns1:rolUsuario"/>
                </dcomcom:RolUsuario>
                <dcomcom:Terminal>
                    <xsl:value-of select="ns0:Envelope/ns0:Body/*/ns1:HEADER/ns1:terminal"/>
                </dcomcom:Terminal>
                <dcomcom:Usuario>
                    <xsl:value-of select="ns0:Envelope/ns0:Body/*/ns1:HEADER/ns1:usuario"/>
                </dcomcom:Usuario>
            </xsl:element>
            <xsl:element name="CabeceraCanalesSol" namespace="{$namespaceDestino}">
                <dcomcom:Aplicacion>
                    <xsl:value-of select="ns0:Envelope/ns0:Body/*/ns1:HEADER/ns1:aplicacion"/>
                </dcomcom:Aplicacion>
                <dcomcom:Canal>
                    <xsl:value-of select="ns0:Envelope/ns0:Body/*/ns1:HEADER/ns1:canal"/>
                </dcomcom:Canal>
                <dcomcom:Fecha>
                    <xsl:value-of select="ns0:Envelope/ns0:Body/*/ns1:HEADER/ns1:fecha"/>
                </dcomcom:Fecha>
                <dcomcom:Nodo>
                    <xsl:value-of select="ns0:Envelope/ns0:Body/*/ns1:HEADER/ns1:nodo"/>
                </dcomcom:Nodo>
                <dcomcom:SesionBG>
                    <xsl:value-of select="ns0:Envelope/ns0:Body/*/ns1:HEADER/ns1:sesionBG"/>
                </dcomcom:SesionBG>
                <dcomcom:DireccionIp>
                    <xsl:value-of select="ns0:Envelope/ns0:Body/*/ns1:HEADER/ns1:direccionIp"/>
                </dcomcom:DireccionIp>
                <dcomcom:OrigenSolicitud>
                    <xsl:value-of select="ns0:Envelope/ns0:Body/*/ns1:HEADER/ns1:origenSolicitud"/>
                </dcomcom:OrigenSolicitud>
            </xsl:element >

            <!-- transformar cuerpo del mensaje -->
            <xsl:for-each select="ns0:Envelope/ns0:Body/*/ns1:BODY/*">
                <xsl:variable name="nombreAtributo" select="local-name()"/>
                <xsl:element name="{$nombreAtributo}" namespace="{$namespaceDestino}">
                    <xsl:choose>
                        <xsl:when test="./*">
                            <xsl:copy-of select="./*"/> 
                        </xsl:when>
                        <xsl:otherwise>
                            <xsl:value-of select="."/> 
                        </xsl:otherwise>
                    </xsl:choose>                
                </xsl:element>
            </xsl:for-each> 
        </xsl:element>

    </xsl:template>
</xsl:stylesheet>